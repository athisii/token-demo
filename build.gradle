plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.cdac'
version '1.0'

repositories {
    mavenCentral()
}

def jarDir = '/usr/share/enrollment/lib/'
def lombokVersion = '1.18.24'


sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.cdac.tokendemo'
    mainClass = 'com.cdac.tokendemo.App'
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.60'

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

}

javafx {
    version = '17.0.10'
    modules = ['javafx.controls', 'javafx.fxml']
}

jar {
    manifest {
        attributes("Main-Class": "com.cdac.tokendemo.App")
        attributes("Class-Path": configurations.runtimeClasspath.files.collect { jarDir + it.getName() }.join(' '))
    }
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}